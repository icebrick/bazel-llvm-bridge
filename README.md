# Bazel to the LLVM Compiler Infrastructure bridge

[Bazel](https://docs.bazel.build/versions/master/bazel-overview.html) is an open-source
build and test tool similar to Make, Maven, and Gradle. It uses a human-readable,
high-level build language. Bazel supports projects in multiple languages and builds outputs
for multiple platforms. For example, Tensorflow - end-to-end open-source platform for
machine learning - uses Bazel as the build system.

[LLVM](https://llvm.org/) is a collection of modular and reusable compiler and toolchain
technologies. The LLVM Core libraries provide a modern source- and target-independent
optimizer and code generator that makes it easy to invent a new programming language or
port an existing compiler. Also LLVM provides its own native C/C++ compiler - Clang,
debugger - LLDB, an implementation of the C++ Standard Library, and many other interesting
things. The full list of LLVM's primary sub-projects is available on the official web-site.

**bazel-llvm-bridge** provides a bridge that let you use static libraries from a local
installation or kindly downloaded archive of LLVM in your projects when Bazel is used
as a building tool. Each library from LLVM/Clang (including the special 'headers' library
that provides LLVM's and Clang's headers) is available as a `@local_llvm//:llvm_<library_name>`
dependency (`@local_llvm//:llvm_headers` for the headers library), where `@local_llvm` is
the name of the used `llvm_configure`
[repository rule](https://docs.bazel.build/versions/master/skylark/repository_rules.html)
while `llvm_` and `clang_` are the default prefixes for LLVM and Clang specific rules,
all these parameters can be configured in your `WORKSPACE`. Notice that a library will
bring also its dependencies exactly how the *CMake* build works.

When LLVM/Clang is built on an environment that supports the
[*Z3 Solver*](https://github.com/Z3Prover/z3), the code of some LLVM and Clang libraries
is enriched by Z3 and projects dependent on the libraries must be linked against it.
To enable the linking, an environment variable is added: `Z3_INSTALL_PREFIX`.
If the environment variable is set, the Z3 library will be copied into the building
directory and a target for the Z3 Solver will be added into the `deps` attribute of the
`llvm_support` library.

Platform-provided libraries such as "ncurses", "tinfo", "pthreads" will be detected
automatically using the `*.cmake` files generated by CMake during the LLVM build process.

Notice: The minimum supported version of Bazel is
[**0.25.0**](https://github.com/bazelbuild/bazel/releases/tag/0.25.0).


### How to leverage the bridge in your project

In order to use any LLVM libraries in your targets, add the following to your
`WORKSPACE` file:

```bzl
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "bazel_llvm_bridge",
    sha256 = "9576aad44d9732e2bd2f33f0e36a2baafe3de846551aaa38e206563dc774f5c2",
    strip_prefix = "bazel-llvm-bridge-release-9-01",
    url = "https://github.com/samolisov/bazel-llvm-bridge/archive/release/9-01.zip",
)

load("@bazel_llvm_bridge//llvm:llvm_configure.bzl", "llvm_configure")

llvm_configure(
    name = "local_llvm",
    llvm_prefix = "llvm_",
    clang_prefix = "clang_",
)
```

Where `name` is whatever you want, default values for `llvm_prefix` and `clang_prefix`
may be omitted. There are three ways to use the library: the first, primarily, is shown
above: the LLVM libraries are fetched from a local installation so the `LLVM_INSTALL_PREFIX`
environment variable **must** points to the local installation.

The second way means the library should download a binary LLVM package and generate
a BUILD file for it:

```bzl
load("@bazel_llvm_bridge//llvm:llvm_configure.bzl", "llvm_configure")

llvm_configure(
    name = "local_llvm",
    llvm_prefix = "llvm_",
    clang_prefix = "clang_",
    sha256 = "c80b5b10df191465df8cee8c273d9c46715e6f27f80fef118ad4ebb7d9f3a7d3",
    strip_prefix = "clang+llvm-9.0.0-x86_64-linux-sles11.3",
    urls = [
        "http://releases.llvm.org/9.0.0/clang+llvm-9.0.0-x86_64-linux-sles11.3.tar.xz"
    ],
)
```

The third way means the library should download a source LLVM package and use a specified
in the `build_file` parameter BUILD file to make a binary package feasible. An optional
`config_file` parameter can be used to specify a Bazel extension file (a `.bzl` file)
with a specification of the LLVM package (containing a list of supported targets,
for example).

```bzl
load("@bazel_llvm_bridge//llvm:llvm_configure.bzl", "llvm_configure")

llvm_configure(
    name = "local_llvm",
    llvm_prefix = "llvm_",
    clang_prefix = "clang_",
    build_file = "//:llvm.autogenerated.BUILD",
    config_file = "//:llvm_config.bzl",
    sha256 = "345dcab760b0e13f1df1b57efa0651dbaae300187bc1556379b562b85bec9e11",
    strip_prefix = "llvm-f49c8ae0e54da8a5b931c1195d4021198b4e6f2f",
    urls = [
        "https://mirror.bazel.build/github.com/llvm-mirror/llvm/archive/f49c8ae0e54da8a5b931c1195d4021198b4e6f2f.tar.gz",
        "https://github.com/llvm-mirror/llvm/archive/f49c8ae0e54da8a5b931c1195d4021198b4e6f2f.tar.gz",
    ],
)
```

Notice: if the `LLVM_INSTALL_PREFIX` environment variable is set, the library means the first described
way of using regardless the presence of the other parameters.

The library's version number is matched to the version of LLVM and can be found in the
`release/<version>-<build>` tag or `release/<version>.x` branch. For example:

```bash
$ git clone https://github.com/samolisov/bazel-llvm-bridge.git
$ git checkout release/9.x
$ git log --oneline -1
```

Shows you the latest commit in the release branch. The commit can be used as a value of the `commit`
attribute of the `git_repository` rule to checkout the compatible version of **bazel-llvm-bridge** for
the used version of **LLVM**:

```bzl
git_repository(
    name = "bazel_llvm_bridge",
    commit = "<THE LATEST COMMIT HERE>",
    remote = "https://github.com/samolisov/bazel-llvm-bridge.git",
)
```

Now the desired libraries can be added as dependencies (the `deps` attribute) to targets for
your libraries and binaries (through the `@local_llvm` repository):

```bzl
cc_binary(
    name = 'llvm_bb_counter',
    srcs = [
        "llvm/llvm_bb_counter.cc",
    ],
    deps = [
        "@local_llvm//:llvm_headers",
        "@local_llvm//:llvm_bit_reader",
    ],
    visibility = ["//visibility:private"],
)
```

### How to build the project

To build your targets, do the following:

 0. Build from sources or download from http://releases.llvm.org/download.html an
    archive with an LLVM package for your platform. The package must be unarchived
    into a local directory.

 1. Set up the `LLVM_INSTALL_PREFIX` environment variable. The variable must
    contain a path to a local LLVM installation:

    ```bash
    $ export LLVM_INSTALL_PREFIX=~/dev/llvm_master
    ```

    or, on Windows:

    ```bash
    $ set LLVM_INSTALL_PREFIX=C:\Dev\llvm_master
    ```

 2. (Optional) Set up the `Z3_INSTALL_PREFIX` environment variable. The variable must
    contain a path to a local Z3 Solver installation:

    ```bash
    $ export Z3_INSTALL_PREFIX=/usr/local
    ```

    or, on Windows:

    ```bash
    $ set Z3_INSTALL_PREFIX="C:\Program Files\z3"
    ```

 3. Run the build:

    ```bash
    $ bazel build //:llvm_bb_counter
    ```

    alternatively, the environment variables may be passed directly to the build
    command:

    ```bash
    $ bazel build --action_env LLVM_INSTALL_PREFIX=C:\Dev\llvm_master
      --action_env Z3_INSTALL_PREFIX="C:\Program Files\z3" //:clang_list_methods
    ```

### How to deal with targets

The **bazel-llvm-bridge** supports all out-of-the-box provided LLVM targets:
 * AArch64
 * AMDGPU
 * ARM
 * BPF
 * Hexagon
 * Lanai
 * Mips
 * MSP430
 * NVPTX
 * PowerPC
 * RISCV
 * Sparc
 * SystemZ
 * WebAssembly
 * X86
 * XCore

To link against a target, the user should to check whether the target is supported by the used
local installation of LLVM. A set of `if_has_<TARGET>` functions are provided by
`@local_llvm//:llvm_config.bzl` generated skylark file. Every function returns its first argument
when the target `<TARGET>` is supported, otherwise it returns the second argument. The function
is usable through the following way (see the `//:llvm_print_supported_targets` target in
the `examples/BUILD` file):

```bzl
load("@local_llvm//:llvm_config.bzl",
    "llvm_copts",
    "if_has_aarch64",
    ...
    "if_has_x86")

cc_binary(
    name = "llvm_print_supported_targets",
    srcs = [
        ...
    ],
    copts = llvm_copts()
        + if_has_aarch64(["-DLLVM_SUPPORTS_TARGET_AARCH64"])
        ...
        + if_has_x86(["-DLLVM_SUPPORTS_TARGET_X86"]),
    deps = if_has_aarch64([
        "@local_llvm//:llvm_aarch64_asm_parser",
        "@local_llvm//:llvm_aarch64_code_gen",
        "@local_llvm//:llvm_aarch64_disassembler",
    ]) +
    ...
       + if_has_x86([
        "@local_llvm//:llvm_x86_asm_parser",
        "@local_llvm//:llvm_x86_code_gen",
        "@local_llvm//:llvm_x86_disassembler",
    ])
)
```

### How to deal with Clang/LLVM shared libraries

Important note for users of the `libclang`, `libclang-cpp` and `LLVM-C` shared
libraries. There are three rules to bring these libraries from the LLVM
installation into the `bazel-bin`: `clang_copy_libclang`, `clang_copy_libclang_cpp`
and `llvm_copy_c`. Just add the targets into the `data` attribute of a rule and
they will appear in a `bazel-bin/external/<name of llvm_configure repository rule>`
directory. Then the libraries can be copied into the `bazel-bin` (see genrules
in the `examples/BUILD` file) and be used for running your applications.
